//===== Hercules Script ======================================
//= Morroc Guides // [TRADUZIDO]
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.4a
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Morroc
//===== Additional Comments: =================================
//= 1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Removed Duplicates [Silent]
//= 1.4 rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.4a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

morocc,153,286,6	script	Guia#moc::MocGuide	4_M_MOC_SOLDIER,{
	cutin "moc_soldier",2;
	mes "[Guia de Morroc]";
	mes "Bem vindo à Morroc, a cidade do deserto.";
	mes "Nós estamos aqui para ajudar os visitantes da cidade. Se você tem alguma dúvida, consulte-nos.";
	mes "Se esta é a sua primeira vez utilizando um guia, por favor selecione 'Tutorial'.";
	.@loop1 = 1;
	while (.@loop1) {
		next;
		switch(select("Indicações","Limpar indicações","Tutorial","Cancelar.")) {
		case 1:
			mes "[Guia de Morroc]";
			mes "Por favor, selecione uma das opções.";
			if (.@compass_check == 0) {
				mes "Você gostaria que as indicações permaneçam no mini-mapa?";
				next;
				if (select("Sim.","Não.") == 1) .@compass_check = 1;
			}
			.@loop2 = 1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) .@wait_button_chk = 1;
				else next;
				
				switch (select ("^FF0000Guilda dos Gatunos.^000000","Loja de Armas","Estalagem.","Bar.","Covil","Cancelar.")) {
				case 1:
					mes "[Guia de Morroc]";
					mes "Ouvi dizer que a Guilda dos Gatunos, o lugar aonde Aprendizes podem se tornar Gatunos, se encontra na Pirâmide da saída Noroeste da cidade.";
					if (.@compass_check)
						viewpoint 1,24,297,2,0xFF0000;
					break;
				case 2:
					mes "[Guia de Morroc]";
					mes "A 'Loja de Armas' se encontra na ponta sudeste da cidade.";
					if (.@compass_check)
						viewpoint 1,253,56,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia de Morroc]";
					mes "As 'Estalagens' se encontram ao sul e a nordeste do centro da cidade.";
					if (.@compass_check) {
						viewpoint 1,197,66,4,0xFF00FF;
						viewpoint 1,273,269,5,0xFF00FF;
					}
					break;
				case 4:
					mes "[Guia de Morroc]";
					mes "O 'Bar' se encontra a noroeste do centro da cidade.";
					if (.@compass_check)
						viewpoint 1,52,259,6,0xFF00FF;
					break;
				case 5:
					mes "[Guia de Morroc]";						
					mes "O 'Covil' se encontra na ponta leste da cidade.";
					if (.@compass_check)
						viewpoint 1,284,171,7,0x00FF00;
					break;
				case 6:
					mes "[Guia de Morroc]";
					mes "Se você deseja apagar as indicações do seu mini-mapa, por favor selecione 'Limpar indicações'.";	
					.@loop2 = 0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0x00FF00;
			viewpoint 2,237,41,3,0x0000FF;
			viewpoint 2,46,345,4,0x00FF00;
			viewpoint 2,175,220,5,0xFF0000;
			viewpoint 2,175,220,6,0xFF0000;
			viewpoint 2,175,220,7,0xFF0000;
			.@compass_check = 0;
			break;
		case 3:
			mes "[Guia de Morroc]";
			mes "Nós fomos exaustivamente treinados para serví-lo da melhor maneira possível e sem falhas.";
			mes "Assim você tem a garantia das melhores indicações possiveis.";
			next;					
			mes "[Guia de Morroc]";
			mes "As indicações que lhe daremos podem ser marcadas no seu mini-mapa.";
			mes "Para visualizar o seu mini-mapa, aperte 'ctrl+tab', ou clique no botão 'mapa' no menu principal.";
			mes "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			mes "[Guia de Morroc]";
			mes "Espero que você tenha uma jornada segura e aproveite sua estadia.";
			close2;
			.@loop1 = 0;
		}
	}
	cutin "moc_soldier",255;
	end;
}

morocc,54,97,0	duplicate(MocGuide)	Guia#2moc	4_M_MOC_SOLDIER
