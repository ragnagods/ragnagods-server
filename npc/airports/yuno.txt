//===== Hercules Script ======================================
//= Yuno Airport Staff // [TRADUZIDO]
//===== By: ==================================================
//= L0ne_W0lf, Muad_Dib
//===== Description: =========================================
//= Yuno Airport Staff
//===== Additional Comments: =================================
//= Official NPCs.
//============================================================

y_airport,143,43,5	script	Assist. do Aeroporto#1y::y_air1	4_F_01,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao Aeroporto de Juno.";
	mes "Aqui você pode ir para Einbroch, Lighthalzen ou Hugel";
	next;
	if (select("Embarcar.:Cancelar.") == 1) {
		mes "[Assistente do Aeroplano]";
		mes "A passagem custa 1.200 zeny.";
		mes "Deseja comprar uma?";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "y_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				Zeny -= 1200;
				warp "y_airport",148,51;
				end;
			}
			mes "[Assistente do Aeroplano]";
			mes "Me desculpe, mas você não possui 1.200 zeny!";
			mes "Você não pode embarcar.";
			close;
		}
	}
	mes "[Assistente do Aeroporto]";
	mes "Muito obrigada!";
	mes "Volte sempre.";
	close;
}

y_airport,158,43,5	duplicate(y_air1)	Assist. do Aeroporto#2y	4_F_01
y_airport,126,43,5	duplicate(y_air1)	Assist. do Aeroporto#3y	4_F_01

y_airport,143,49,3	script	Assist. do Aeroporto#4y::y_air2	4_F_01,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao Aeroporto de Juno.";
	mes "Deixe-me te ajudar a chegar no terminal principal!";
	mes "Caso deseje ir para Einbroch, Lighthalzen, Hugel, Izlude ou Rachel por favor embarque novamente no Aeroplano";
	next;
	if (select("Sair ao terminal.:Cancelar.") == 1) {
		mes "[Assistente do Desembarque]";
		mes "Uma vez no terminal principal, você terá que pagar novamente para embarcar a outra cidade.";
		mes "Apenas saia caso esteja seguro da decisão!";
		mes "Deseja continuar?";
		next;
		if (select("Sim:Não") == 1) {
			warp "y_airport",142,40;
			end;
		}
	}
	mes "[Assistente do Desembarque]";
	mes "Muito bem, obrigada pela atenção!";
	mes "Caso deseje voltar, por favor é só me procurar";
	close;
}

y_airport,126,51,3	duplicate(y_air2)	Assist. do Aeroporto#5y	4_F_01
y_airport,158,50,3	duplicate(y_air2)	Assist. do Aeroporto#6y	4_F_01

y_airport,145,63,5	script	Aux. Vôos Domésticos	4_F_02,{
	mes "[Aux. de Vôos Domésticos]";
	mes "Gostaria de embarcar no Aeroplano que vai para Einbroch, Lighthalzen ou Hugel?";
	mes "Se sim, deixe-me te indicar a área de embarque.";
	next;
	if (select("Sim.:Não.") == 1) {
		warp "yuno",59,244;
		end;
	}
	mes "[Aux. Vôos Domésticos]";
	mes "Muito bem.";
	mes "Obrigada pela sua atenção, caso mude de idéia basta me procurar.";
	close;
}

y_airport,140,63,5	script	Aux Vôos Internacionais	4_F_02,{
	mes "[Aux. Vôos Internacionais]";
	mes "Gostaria de embarcar no Aeroplano que vai para Juno, Izlude e Rachel?";
	mes "Se sim, deixe-me te indicar a área de embarque.";
	next;
	if (select("Sim.:Não.") == 1) {
		warp "yuno",47,244;
		end;
	}
	mes "[Aux. Vôos Internacionais]";
	mes "Muito bem.";
	mes "Obrigada pela sua atenção, caso mude de idéia basta me procurar.";
	close;
}

yuno,14,262,5	script	Assist. do Aeroporto#y1	4_F_02,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao Aeroporto de Juno.";
	mes "Utilize esta porta para embarcar no";
	mes "Aeropano com destino a Izlude, no";
	mes "Reino de Rune-Midgard e para";
	mes "Rachel na República de Arunafeltz.";
	next;
	mes "[Assistente do Aeroporto]";
	mes "Caso Juno seja seu destino,";
	mes "desça as escadas e fale com o Assistente do Aeroporto";
	mes "para levá-lo ao terminal principal.";
	mes "Obrigada, e aproveite sua viagem.";	
	close;
}

yuno,88,263,3	script	Assist. do Aeroporto#y2	4_F_02,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao Aeroporto de Juno.";
	mes "Utilize esta porta para embarcar no";
	mes "Aeropano com parada em Einbroch, Lighthalzen e";
	mes "Hugel na República de Schwaltzvalt.";
	next;
	mes "[Assistente do Aeroporto]";
	mes "Caso Juno seja seu destino,";
	mes "desça as escadas e fale com o Assistente do Aeroporto";
	mes "para levá-lo ao terminal principal.";
	mes "Obrigada, e aproveite sua viagem.";
	close;
}
