//===== Hercules Script ======================================
//= Niflheim Script // [TRADUZIDO]
//===== By: ==================================================
//= Fyrien, Dizzy, PKGINGO
//= Official NPCs translated and re-edited by Celest
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= Official NPC's for Niflheim
//= 1.01 Splitted file(guides, shops). Fixed rand() bugs,
//=      missing labels bugs, optimization [Lupus]
//= 1.03 fixed end; -> close;
//= 1.04-1.05 fixed several bugs with missing CLOSE button, wrong var name
//=     in Piano Keys quest
//=     fixed zeny/item, exploit in Sairin, some optimizations [Lupus]
//= 1.06 some typos in some variables fixed (thx 2 Dr.Evil) [Lupus]
//= 1.07 Fixed typos [Nexon]
//= 1.08 Commented "Traveler" out as it is not official & we have correct Bungee quest in.
//=     Also removed TODO, as they are already done. [Vicious_Pucca]
//= 1.1 Replaced nif_q_done with MISC_QUEST & 32. nif_t -> @nif_t [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Moved Book of Devil and Piano quest to quests/quests_niflheim.txt [Evera]
//= 1.4 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Removed Cursed Spirits as they were custom.
//= 1.5 Added dummy "OnMyMobDead" event to Cursed Spirit. [L0ne_W0lf]
//============================================================

niflheim,52,147,3	script	Kurse#nif	4_M_NFDEADSWDMAN,{
	mes "[Kurse]";
	mes "Os vivos não podem entrar nesse local.";
	mes "Não! Eu me recuso a permitir";
	mes "a passagem de seres vivos!";
	close;
}

niflheim,195,211,6	script	Kikandia#nif	4_M_NFDEADSWDMAN,3,3,{
	if (isequipped(5038) || isequipped(2257) || isequipped(2256)) {
		mes "[" + strcharinfo(0) + "]";
		mes "O que posso fazer por você? ";
		next;
		mes "[Kikandia]";
		mes "Sim, fui eu mesmo quem te chamei.";
		mes "Vamos, venha até aqui!";
		next;
		mes "[Kikandia]";
		mes "Olha, você é muito descuidado. Seus chifres...";
		mes "Eles estão caídos. Devia manter sempre bem arrumados e polidos.";
		mes "Você sabe, o Senhor dos Mortos está de olho em você. ";
		close;
	}
	mes "[Kikandia]";
	mes "O senhor dos mortos vê tudo.";
	close;

OnTouch:
	if (isequipped(5038) || isequipped(2257) || isequipped(2256)) {
		mes "[Gigantia]";
		mes "Espere um pouco!";
		close;
	}
	end;
}

nif_in,16,27,1	script	Galinha Morta#nif	4_NFCOCK,{
	percentheal -5,0;
	mes "[Assado]";
	mes "Cocó! Fui comida por um humano, num jantar!";
	mes "Agora é minha vez de comer você! CoCoóóCoó. ";
	mes "Ahaha! Nunca pensei que este mundo fosse tão divertido. Coócocoó. ";
	close;
}

nif_in,16,30,4	script	Morcego Vampiro Morto#nf	4_NFBAT,{
	percentheal -5,0;
	mes "[Bathoman]";
	mes "Sangue de humano é bom, estejam eles,";
	mes "mortos ou vivos. Hmmm...";
	mes "Você parece bem saudável. ";
	mes "Posso dar uma mordidinha em você? ";
	close;
}

nif_in,156,93,5	script	Ana Arcana#nif	4_F_NFLOSTGIRL,{
	mes "[Ana Arcana]";
	mes "Q-Que lugar é esse!?";
	mes "Eu acordei neste lugar escuro... Mamãe? ";
	mes "Eu quero minha mamãe! Huh...? Buáááá... ";
	mes "Quero ir para casa!";
	next;
	switch (select ("Sobre a feiticeira...:Sobre essa maldição... :Ir embora")) {
	case 1:
		mes "[Ana Arcana]";
		mes "Feiticeira...? Não conheço nenhuma...";
		mes "Minha mamãe lia sobre feiticeiras nos contos.";
		mes "Mas não sei se existe mesmo uma feiticeira.";
		mes "Se ela existir mesmo, deve ser muito má...";
		next;
		mes "[Ana Arcana]";
		mes "Mas por que está tão frio aqui...?";
		if (Sex == 1)
		{
			mes "Você não está com frio, tio? Que estranho...";
		}
		else
		{
			mes "Você não está com frio, tia? Que estranho...";
		}
		mes "Quero minha casa...";
		mes "P-Por favor... Me ajuda! Por favor!";
		close;
	case 2:
		mes "[Ana Arcana]";
		mes "Maldição... Eu lembro da vovó falando sobre";
		mes "isso antes. Que havia um jeito de fugir da";
		mes "maldição que não se cura nem com bênção. ";
		mes "Uma maldição muito terrível... ";
		next;
		mes "[Ana Arcana]";
		mes "Klaatu(Klaatu)...";
		mes "Verata(Verata).....";
		mes "E então... Hmm... Qual era a última? ";
		mes "Oh, eu não me lembro da palavra mágica! ";
		next;
		mes "[Ana Arcana]";
		mes "Ela disse que eu devia lembrar sempre,";
		mes "que devia falar as três palavras em ordem para escapar.";
		mes "Mas não lembro a terceira palavra.";
		mes "Eu esqueci!";
		close;
	case 3:
		mes "[Ana Arcana]";
		mes "Está tão frio... Tão frio... Quero minha mamãe. ";
		mes "Por favor... Me leve com você... ";
		mes "Estou com muito medo aqui...";
		mes "Por favor! Me salve deste lugar...";
		close;
	}
}

niflheim,350,258,1	script	Espírito Amaldiçoado#nif	4_NFWISP,{
	killmonster "niflheim","Espírito Amaldiçoado#nif::OnMyMobDead";
	mes "[Eshu Bruce]";
	mes "Eu posso sentir uma maldição terrível em você... ";
	mes "Saia daqui! Não se aproxime";
	mes "desta biblioteca ou eu te mato!!!";
	mes "Não toque nos meus livros!!";
	next;
	switch(select("Pegar o primeiro livro:Pegar o segundo livro:Pegar o terceiro livro:Ir embora")) {
	case 1:
		monster "niflheim",349,259,"Rideword",1478,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		mes "[Eshu Bruce]";
		mes "Como ousa tocar em meus livros?";
		mes "MWAHAHAHAH! Morra com sua maldição!! ";
		mes "Você será enterrado aqui e sofrerá mil tormentos!";
		close;
	case 2:
		mes "[Eshu Bruce]";
		mes "Eu disse para não tocar nos meus livros...";
		mes "SAIA!";
		mes "Não atrapalhe meu sono!";
		close2;
		warp "niflheim",34,162;
		end;
	case 3:
		mes "[Eshu Bruce]";
		mes "Mwahahahahaha....";
		mes "Como ousa me amaldiçoar...!! Agora venha...";
		mes "Fale as três palavras da contra-maldição, vamos!";
		emotion e_gg;
		next;
		if (select("Clover:Klaatu:Klaytos") == 2) {
			++.@spell;
		}
		if (select("Verit:Veritas:Verata") == 3) {
			++.@spell;
		}
		if (select("Néctar:Nerus:Nero:^FFFFFFNictu!!!^000000") == 4) {
			++.@spell;
		}
		if (.@spell == 3) {
			switch(rand(1,5)) {
			case 1:
				if (morison_meat < 15) {
					morrison_meat = 15;
					mes "[Eshu Bruce]";
					mes "Hahahaha!!! A maldição está quebrada!";
					mes "MwahaHahaha.....!!!";
					close;
				}
				mes "[Eshu Bruce]";
				mes "Você leu o encantamento corretamente...";
				mes "MAS NÃO O QUE QUEBRA O FEITIÇO! ";
				mes "Mwahahahahaha.....!!!";
				close;
			case 2:
				if (thai_head == 1) {
					thai_head = 2;
					mes "[Eshu Bruce]";
					mes "Hahahaha!!! A maldição está quebrada!";
					mes "MwahaHahaha.....!!!";
					close;
				}
				mes "[Eshu Bruce]";
				mes "Você leu o encantamento corretamente...";
				mes "MAS NÃO O QUE QUEBRA O FEITIÇO! ";
				mes "Mwahahahahaha.....!!!";
				close;
			case 3:
				if (thai_head == 8) {
					thai_head = 7;
					mes "[Eshu Bruce]";
					mes "Hahahaha!!! A maldição está quebrada!";
					mes "MwahaHahaha.....!!!";
					close;
				}
				mes "[Eshu Bruce]";
				mes "Você leu o encantamento corretamente...";
				mes "MAS NÃO O QUE QUEBRA O FEITIÇO! ";
				mes "Mwahahahahaha.....!!!";
				close;
			case 4:
				mes "[Eshu Bruce]";
				mes "Você leu o encantamento corretamente...";
				mes "MAS NÃO O QUE QUEBRA O FEITIÇO! ";
				mes "Mwahahahahaha.....!!!";
				close;
			}
		}
		monster "niflheim",345,259,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		monster "niflheim",347,261,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		monster "niflheim",344,253,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		monster "niflheim",346,251,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		monster "niflheim",349,249,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		monster "niflheim",350,260,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		monster "niflheim",353,256,"Orc Esqueleto",1462,1,"Espírito Amaldiçoado#nif::OnMyMobDead";
		mes "[Eshu Bruce]";
		mes "Hahahahaha!!! O feitiço está errado!";
		mes "Eu avisei! Morra de uma vez aqui!";
		mes "E sofra os mil tormentos...!!";
		close;
	case 4:
		mes "[Eshu Bruce]";
		mes "Mwah Hahahah! Bom trabalho...";
		mes "Espero que você continue vivo... Uhuh...";
		close;
	}

OnMyMobDead:
	end;
}
