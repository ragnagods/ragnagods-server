//===== Hercules Script ======================================
//= Alberta Town // [TRADUZIDO]
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.6
//===== Description: =========================================
//= Town-specific Alberta NPCs
//===== Additional Comments: =================================
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//============================================================

alberta,97,51,0	script	Mercador#alb	4_M_02,{
	mes "[Mercador]";
	mes "Quando você viaja por todo o mundo, você escuta muitos boatos. As vezes escuta algo assim... Há um item chamado Carta que tem poderes de monstros guardados nela.";
	next;
	mes "O boato também diz que ao usar um item assim o dono adquire grande poder... Náá, tenho certeza que é só mais um boato bobo.";
	close;
}

alberta,53,39,0	script	Mercador#alb2	4W_SAILOR,{
	mes "[Mercador]";
	mes "Oh, você parece ser um viajante? Bem-vindo a Alberta. Escute, eu tenho algumas coisas para pensar então, com licença.";
	next;
	mes "Escutei por aí que a Cidade Mágica, Geffen tem uma loja vendendo uma armadura misteriosa que resiste a magia. Se eu conseguir distribuí-la em grandes quantidades, vou ficar rico rapidinho.";
	close;
}

alberta,20,183,0	script	Jovem#alb	1_M_03,{
	mes "[Mercador]";
	mes "Dizem que a arma lendária chamada Gungnir nunca erra o alvo... Se for verdade... será legal, não acha?";
	close;
}

alberta,131,139,2	script	Velho Bêbado#alb	1_M_JOBGUIDER,{
	mes "[Velho Bêbado]";
	mes "-ic-.... Hein?.... Que que cê tá olhando? Suma!!";
	next;
	if(select("Ficar.:Deixe-o sozinho.") == 2) {
		mes "[Velho Bêbado]";
		mes "Isso mesmo! Vá embora!!!";
		close;
	}
	mes "[Velho Bêbado]";
	mes "Hahahaha -ic-... Você é cara-de-pau....";
	mes "he.. eu pareço um Zé Ninguém agora, mas eu era um Marinheiro do infâme 'Going Mary'.";
	next;
	if(select("Que tipo de navio ele é?:Nossa! Legal!") == 2) {
		mes "[Velho Bêbado]";
		mes "O CAPITÃO!!! -IC-! Ele fazia assim com a espada..aí..AÍ,";
		mes "Não somente o alvo mas também os inimigos ao redor dele eram anvoltos em chamas, além da habilidade do capitão, a espada devia ter alguma magia nela.";
		next;
		mes "[Velho Bêbado]";
		mes "Nossa... snif snif.. como tenho saudades de todo mundo... Agora estou deprimido. Vá embora.";
		close;
	}
	mes "[Velho Bêbado]";
	mes "Como é? Nunca ouviu falar?";
	mes "Estupido! Todos conheciam o notório navio pirata 'Going Mary'! -ic-";
	next;
	mes "[Velho Bêbado]";
	mes "Ah... os velhos tempos.. Imagine se não tivessemos ido parar naquela tempestade... -ic-";
	mes "AH... O Capitão... sinto mais saudade dele entre todos... nenhum inimigo sobrevivia a sua espada";
	next;
	mes "[Velho Bêbado]";
	mes "CAAPTÃO!!! -IC- -IC- -IC-! Nós faziamos assim com a espada dele..aí..AÍ,";
	mes "Não somente o alvo mas também os inimigos ao redor dele eram anvoltos em chamas, além da habilidade do capitão, a espada devia ter alguma magia nela.";
	next;
	mes "[Velho Bêbado]";
	mes "Nossa... snif snif.. como tenho saudades de todo mundo... Agora estou deprimido. Vá embora.";
	close;
}

alberta,58,80,2	script	Mercador#alb3	4W_M_03,{
	mes "[Mercador]";
	if (rand(2)) {
		mes "Nós mercadores podemos abrir uma lojinha na beira da estrada e fazer negócios. Com a habilidade Desconto, nós podemos comprar itens muito baratos nas lojas das cidades. Aí carregá-las no nosso carrinho e vendê-las para as pessoas.";
		next;
		mes "[Mercador]";
		mes "Assim, o negócio é mais conveniente e sem erro. Não durma porque é muito fácil.";
	} else {
		mes "Nós mercadores também temos a habilidade para negociar melhor, assim vendemos itens a um preço maior que o normal.";
		mes "Quando as pessoas que querem comprar itens gostem de coisas baratas! Você terá que dar mais dinheiro, por favor!'";
		next;
		mes "[Mercador]";
		mes "Nós podemos conseguir 24% a mais com essa incrível habilidade. Mas lembre-se de treinar duro para desenvolvê-la!!";
	}
	close;
}

alberta,62,156,2	script	Phina#alb	8_F,{
	mes "[Phina]";
	switch(rand(3)){
	case 0:
		mes "Teve uma vez, que eu estava andando por aí quando vi uma graminha verde saindo do chão.";
		mes "Era tããão bonita que eu queria tocá-la. Sabe o que aconteceu? A planta deu um tapa na minha mão.";
    	next;
    	mes "[Phina]";
    	mes "Eu fiquei tããão surpresa que tive de me afastar. Foi aí que percebi que não era uma planta mas um animal bem pequenininho.";
    	mes "Não encoste neles nem por acidente. Os animais mais calmos também podem ser perigosos quando estão bravos.";
		close;
	case 1:
    	mes "Sabe aqueles ursos que parecem uns bobões que vivem na floresta entre Alberta e Payon...";
    	mes "brincando com os insetos.. fazendo barulhinhos?";
    	next;
    	mes "[Phina]";
    	mes "Por diversão, eu joguei um graveto em um. Aí, de repente, ele veio correndo na minha direção, e eu morri de medo.";
		mes "Se eu não me movesse para o lado rapidamente, eu não quero nem pensar no que teria acontecido por que BUM!!! Ele bateu em uma árvore enorme e a a árvore caiu aos pedaços.";
    	mes "Eu aprendi a lição, jamais provocar um animal senão ele pode vir para cima de mim.";
		close;
	case 2:
    	mes "Você sabia? Os lobos são muito mais unidos do que imagina. Se um for atacado, todos os outros virão correndo ajudá-lo.";
    	mes "Você deve tomar cuidado se algum dia quiser atacar um.";
    	close;
	}
}

alberta,93,174,2	script	Vovó#alb	8_F_GRANDMOTHER,{
	mes "[Vovó]";
	mes "Um tempo atrás, um navio bateu no porto de Alberta.";
	mes "Acreditando que podiam haver sobreviventes, alguns jovens da cidade foram ao navio, mas todos correram aterrorizados. Eles disseram que os corpos estão andando pelo navio.";
	next;
	mes "[Vovó]";
	mes "Ah... e criaturas marinhas desconhecidas estavam lá dentro, então os jovens não podiam nem passar pela entrada do navio...";
	next;
	mes "[Vovó]";
	mes "Então não podemos fazer nada sobre o navio misterioso a não ser deixá-lo lá. Nós esperávamos que algum dia ele fosse afundar ou ser levado pela correnteza aí, não sei como, uma empresa chamada Eventos Legais Corp. ouviu boatos sobre o navio.";
	mes "Eles pagaram uma quantia enorme à cidade para deixá-los cuidarem do assunto.";
	next;
	mes "[Vovó]";
	mes "Agora eles recrutam vários jovens para acabar com os monstros. Bem... parece que a Eventos Legais Corp. procura jovens para testar suas habilidades e perícias. Pode ser uma boa experiência para um jovem....";
	mes "Mas não vale a pena arriscar a vida....";
	close;
}

alberta,189,151,5	script	Marinheiro#alb	4W_SAILOR,{
	mes "[Marinheiro]";
	mes "Onde você pretende ir?";
	next;
	switch(select("Navio Fantasma -> 250 zeny.","Marina de Izlude -> 500 zeny.","Sair.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Marinheiro]";
				mes "Ah! Sem essa! eu preciso dos 250 zeny!";
				close;
			}
			Zeny -= 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Marinheiro]";
				mes "Ah! Sem essa! Eu preciso dos 250 zeny!";
				close;
			}
			Zeny -= 500;
			//if(checkre(0))
			//	warp "izlude",195,212;
			//else
				warp "izlude",176,182;
			end;
		case 3:
			mes "[Marinheiro]";
			mes "Faça como quiser....";
			close;
	}
}

alb2trea,39,50,6	script	Marinheiro#2trea	4W_SAILOR,{
	mes "[Marinheiro]";
	mes "Quer voltar?";
	next;
	if(select("Voltar para Alberta.","Sair.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Poch#alb	4_M_04,{
	mes "[Poch]";
	mes "Bom dia, senhoras e senhores. Vocês gostariam de explorar a aventura fantástica do Navio Submerso, o evento especial para pessoas entediadas fornecido por nós, Eventos Legais Corp?";
	mes "Ah! Antes de entrarem. Não se arrisquem se não forem tão fortes.";
	next;
	mes "[Poch]";
	mes "Então, está pronto? Ok então por favor assine aqui.";
	mes "Nossa! A taxa de admissão é 200 Zeny.";
	next;
	switch(select("Entrar","Sair")) {
		case 1:
			if (Zeny < 200) {
				mes "[Poch]";
				mes "Meu caro, você não tem dinheiro o suficiente. Volte quando puder pagar a taxa.";
				close;
			} else {
				Zeny -= 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Poch]";
			mes "Volte depois.";
			close;
	}
}

alberta,190,173,4	script	Phelix	4_M_03,{
	.@weight = MaxWeight-Weight;
	mes "[Phelix]";
	if ((.@weight) < 10000) {
		mes "Espere um momento!!";
		mes "Você trouxe muitas coisas!";
		mes "Você não pode carregar mais itens!";
		mes "Por favor reduza o número de itens,";
		mes "e venha me ver novamente.";
		close;
	}
	if (@event_zelopy == 0) {
   		mes "[Phelix]";
    	mes "Que diabos está fazendo aqui?";
    	mes "Não há nada grátis nesse navio. Se quiser ser recompensado, trabalhe!!";
    	next;
    	mes "[Phelix]";
    	mes "Hmm, mas se me trouxer 10 Jellopy até posso te dar 1 poção.";
    	mes "Bem, se for pedir muito de você, 3 Jellopy servirão por 1 cenoura.";
    	next;
    	mes "[Phelix]";
    	mes "Se estiver interessado me procure depois com as coisas.";
		@event_zelopy = 1;
		close;
	} else {
		mes "Hmm.. você quer trocar jellopy por Poção Vermelha ou Cenoura. O que quer?";
		next;
		switch(select("Poção Vermelha.","Cenoura.")) {
		case 1:
			mes "[Phelix]";
			mes "Ok então deixe-me ver o que trouxe..";
			next;
			mes "[Phelix]";
			if (countitem(909) < 10) {
				mes "Ei! Não é o suficiente. Eu disse 1 Poção Vermelha por 10 Jellopy. Entendeu?";
				close;
			} else {
				.@max = countitem(909)/10;
				mes "Nada mal...";
				mes "Quantas poções quer?";
				next;
				switch(select("Quantas eu tiver direito.","Te direi.","Nenhuma.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;
				case 2:
					mes "[Phelix]";
					mes "Escreva um número menor que 100, OK? Se mudar de ideia escreva '0'.";
					mes "Posso te dar um máximo de... " + .@max + " pelo o que tem.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount <= 0) {
						mes "Volte depois quando quiser.";
						close;
					}
					if (.@amount > 100) {
						mes "Mais que 100 é demais. Escolha outro número.";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Bem bem, você não tem o suficiente em Jellopy.";
						mes "Volte quando tiver mais Jellopy";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[Phelix]";
					mes "OK, até a próxima.";
					close;
				}
				mes "[Phelix]";
				mes "Aqui estão! Poções como prometido. Use-as sabiamente.";
				close;
			}
		case 2:
			mes "[Phelix]";
			mes "Deixe-me ver o que trouxe..";
			next;
			mes "[Phelix]";
			if (countitem(909) < 3) {
				mes "Ei! Não é o suficiente. Eu disse 1 Cenoura por 3 Jellopy.. Entendeu?";
				close;
			} else {
				.@max = countitem(909)/3;
				mes "Quantas batatas vai querer?";
				next;
				switch(select("Quantas tiver direito.","Prefiro escrever um número.","Nenhuma.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[Phelix]";
					mes "Escreva um número menor que 100. Se mudar de ideia escreva '0'.";
					mes "Posso te dar um máximo de... " + .@max + " pelo o que tem.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount == 0) {
						mes "Até a próxima.";
						close;
					}
					if (.@amount > 100) {
						mes "Escreva outro número, mais que 100 é demais.";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Bem bem, você não tem Jellopy o suficiente.";
						mes "Traga-me mais Jellopy";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[Phelix]";
					mes "Volte depois.";
					close;
				}
				mes "[Phelix]";
				mes "Aqui estão! Cenouras como prometido. Use-as sabiamente.";
				close;
			}
		}
	}
}
