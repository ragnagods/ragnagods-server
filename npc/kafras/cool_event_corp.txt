//===== Hercules Script ======================================
//= Cool Event Corp. Staff // [TRADUZIDO]
//===== By: ==================================================
//= Gepard
//===== Current Version: =====================================
//= 2.0
//===== Description: =========================================
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: =================================
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 2.0 Moved duplicate code to a function. Better integrated
//      the rest with functions_kafra.txt. [Haru]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which menu to display.
//=   arg(1): Used to determine the location displayed in the save message.
//==========================================================================
function	script	F_ZondaStaff	{
	cutin "zonda_01",2;
	mes "[Funcionário Corp. Eventos Incríveis]";
	mes "Bem-vindo à Corporação Eventos Incríveis.";
	mes "Nossa equipe está sempre trabalhando";
	mes "para corresponder às suas expectativas";
	mes "referentes à qualidade do serviço prestado. Então";
	mes "como posso ajudá-l"+(sex == 1?"o":"a")+" hoje?";
	next;
	switch (getarg(0)) {
		// Save, Storage, Pushcart, Storage Password (Hugel)
		case 0: setarray @Z_Menu0$, "Salvar", "Usar Armazém", "Alugar um Carrinho", "Serviço de Senha no Armazém", "Cancelar"; break;
		// Save, Storage, Pushcart, Storage Password (Veins, Rachel)
		default:
		case 1: setarray @Z_Menu0$, "Salvar", "Usar Armazém", "Usar Teleporte", "Alugar um Carrinho", "Serviço de Senha no Armazém", "Cancelar"; break;
	}
	.@menu$ = "";
	for (.@i = 0; .@i < getarraysize(@Z_Menu0$); ++.@i)
		.@menu$ += @Z_Menu0$[.@i] + ":";
	.@j = select (.@menu$) - 1;
	if (@Z_Menu0$[.@j] == "Save") {
		mes "[Funcionário Corp. Eventos Incríveis]";
		mes "Seu Ponto de Retorno";
		mes "foi salvo aqui";
		mes getarg(1);
		mes "Obrigado por utilizar";
		mes "os Serviços da Corporação Eventos Incríveis!";
		return;
	} else if (@Z_Menu0$[.@j] == "Usar Armazém") {
		callfunc "F_KafStor",0,40,6,"[Funcionário Corp. Eventos Incríveis]";
		close2;
		cutin "", 255;
		end;
	} else if (@Z_Menu0$[.@j] == "Usar Teleporte") {
		callfunc "F_KafTele", 6, "[Funcionário Corp. Eventos Incríveis]";
		close2;
		cutin "", 255;
		end;
	} else if (@Z_Menu0$[.@j] == "Alugar um Carrinho") {
		callfunc "F_KafCart", 6, 800, "[Funcionário Corp. Eventos Incríveis]";
		close2;
		cutin "", 255;
		end;
	} else if (@Z_Menu0$[.@j] == "Serviço de Senha no Armazém") {
		callfunc("F_SetKafCode","[Funcionário Corp. Eventos Incríveis]","Corp. Eventos Incríveis");
		//the function closes msg and removes cutins
	} else {
		mes "[Funcionário Corp. Eventos Incríveis]";
		mes "A Corporação Eventos Incríveis está sempre";
		mes "buscando fornecer os melhores";
		mes "serviços aos nossos clientes.";
		mes "Ajude-nos a nos tornarmos os melhores,";
		mes "nos dando suas sinceras opiniões";
		mes "e sugestões. Obrigado.";
		close2;
	}
	cutin "", 255;
	end;
}

hugel,88,168,4	script	Corp. Eventos Incríveis::CoolEventCorpStaffHugel	4_M_ZONDAMAN,{
	callfunc "F_KafSet";
	callfunc "F_ZondaStaff", 0, "na vila de Hugel.";
	savepoint "hugel",96,155;
	close2;
	cutin "", 255;
	end;
}

rachel,109,138,5	script	Corp. Eventos Incríveis::CoolEventCorpStaffRachel	4_M_ZONDAMAN,{
	callfunc "F_KafSet";
	callfunc "F_ZondaStaff", 1, "na cidade de Rachel.";
	savepoint "rachel",113,137;
	close2;
	cutin "", 255;
	end;
}

veins,208,128,5	script	Corp. Eventos Incríveis::CoolEventCorpStaffVeins	4_M_ZONDAMAN,{
	callfunc "F_KafSet";
	callfunc "F_ZondaStaff", 1, "na cidade de Veins.";
	savepoint "veins",204,103;
	close2;
	cutin "", 255;
	end;
}
